<!DOCTYPE html>
<html>
<head>
<title>gallery</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
<meta http-equiv="Pragma" content="no-cache"/>
<meta http-equiv="Expires" content="-1"/>
<style>
   .toggledOn { font-weight: bold; }
   .resize { max-height: 850px; width: auto; }
</style>
</head>
<body>

<div id="navDiv">
   <span id="begin">begin</span>
   <span id="previous">previous</span>
   <span id="next">next</span>
   <span id="end">end</span>
   <span id="toggleRandom">toggleRandom</span> <!-- next or previous instead picks a random image -->
   <span id="toggleResize">toggleResize</span>
   <span id="toggleSlideshow">toggleSlideshow</span> <!-- auto calls next -->
   <span id="toggleWrap">toggleWrap</span> <!-- click previous when at beginning will go to end, and vice-versa -->
   <span id="currently"></span>
</div>

<div id="imageDiv"/>

<script type="text/coffeescript">
      # Define the model for Links.
      LinkModel = Backbone.Model.extend(
         url: () ->
            @get('link')
      )

      # Define what a collection of Links is.
      LinksCollection = Backbone.Collection.extend(
         model: LinkModel
      )

      # Define the model for the Gallery application.
      # Note: the defaults: {} behavior of Model doesn't work well?
      GalleryAppModel = Backbone.Model.extend(
         initialize: () ->
            @links = new LinksCollection()
            @set('index', 0, silent: true)
            @set('random', false, silent: true)
            @set('resize', false, silent: true)
            @set('slideshow', false, silent: true)
            @set('wrap', false, silent: true)
            
            interval(4000, () =>
               if @isSlideshow()
                  @increment()
            )
            
         begin: () ->
            @set('index', 0)

         # Returns a LinkModel.
         currentLink: () ->
            @links.at(@get('index'))

         # Returns a number between 0 and length() - 1, inclusive.
         currentPosition: () ->
            @get('index')

         decrement: () ->
            if @isRandom()
               @setRandomIndex()
            else
               if @currentPosition() == 0
                  if @isWrap()
                     @end()
               else
                  @set('index', @get('index') - 1)

         end: () ->
            @set('index', @length() - 1)

         # Sets the url on the internal collection, fetches it, sets the current index to 0, returns a promise (not in that order).
         load: (loadUrl) ->
            @links.url = loadUrl
            @links.fetch(
               reset: true
               success: () => # fat arrow
                  @begin()
            )

         increment: () ->
            if @isRandom()
               @setRandomIndex()
            else
               if @currentPosition() == (@length() - 1)
                  if @isWrap()
                     @begin()
               else
                  @set('index', @get('index') + 1)
                  
         length: () ->
            @links.length

         # Will be true or false.
         isRandom: () ->
            @get('random')

         # Private method.
         setRandomIndex: () ->
            if @length() > 1
               loop
                  randomPosition = Math.floor(Math.random() * @length())
                  if randomPosition != @currentPosition()
                     @set('index', randomPosition)
                     return

         toggleRandom: () ->
            @set('random', not @get('random'))

         # Will be true or false.
         isResize: () ->
            @get('resize')

         toggleResize: () ->
            @set('resize', not @get('resize'))

         # Will be true or false.
         isSlideshow: () ->
            @get('slideshow')

         toggleSlideshow: () ->
            @set('slideshow', not @get('slideshow'))

         # Will be true or false.
         isWrap: () ->
            @get('wrap')

         toggleWrap: () ->
            @set('wrap', not @get('wrap'))
      )

      # Define the view for the image/photo.
      ImageView = Backbone.View.extend(
         el: imageDiv # Can't have quotes here.

         initialize: (@galleryAppModel) ->
            @listenTo(@galleryAppModel, 'change', @render)
            @render()

         render: () ->
            img = $('<img>',
               'class': if @galleryAppModel.isResize() then 'resize' else ''
               'src': @galleryAppModel.currentLink().url()
            )
            @$el.html(img)
      )

      # Define the view for the navigation bar.
      NavView = Backbone.View.extend(
         el: navDiv # Can't have quotes here.

         initialize: (@galleryAppModel) ->
            @listenTo(@galleryAppModel, 'change', @render)
            @render()

         events:
            'click #begin': () ->
               @galleryAppModel.begin()

            'click #end': () ->
               @galleryAppModel.end()

            'click #next': () ->
               @galleryAppModel.increment()

            'click #previous': () ->
               @galleryAppModel.decrement()

            'click #toggleRandom': () ->
               @galleryAppModel.toggleRandom()

            'click #toggleResize': () ->
               @galleryAppModel.toggleResize()

            'click #toggleSlideshow': () ->
               @galleryAppModel.toggleSlideshow()

            'click #toggleWrap': () ->
               @galleryAppModel.toggleWrap()

         render: () ->
            if @galleryAppModel.isRandom() then $('#toggleRandom').addClass('toggledOn') else $('#toggleRandom').removeClass('toggledOn')
            if @galleryAppModel.isResize() then $('#toggleResize').addClass('toggledOn') else $('#toggleResize').removeClass('toggledOn')
            if @galleryAppModel.isSlideshow() then $('#toggleSlideshow').addClass('toggledOn') else $('#toggleSlideshow').removeClass('toggledOn')
            if @galleryAppModel.isWrap() then $('#toggleWrap').addClass('toggledOn') else $('#toggleWrap').removeClass('toggledOn')
            
            $('#currently').text("(#{ @galleryAppModel.currentPosition() + 1 } of #{ @galleryAppModel.length() })")
      )

      # Function to get keys/values of the url.
      getSearchKeyValues = () ->
         result = {}
         splitSearch = window.location.search.substring(1).split('&');
         for pair in splitSearch
            [name, val] = pair.split('=')
            result[name] = decodeURIComponent(val)
         return result
      
      # Function to make setting intervals nicer.
      interval = (ms, callback) ->
         setInterval(callback, ms)
      
      # Wait for the web page to be fetched, then load the json, then create the views (which automatically renders them).
      $(() ->
         galleryAppModel = new GalleryAppModel()
         jsonUrl = getSearchKeyValues()['json']
         promise = galleryAppModel.load(jsonUrl)

         promise.then(
            (data, textStatus, jqXHR) ->
               navView = new NavView(galleryAppModel)
               imageView = new ImageView(galleryAppModel)

            (jqXHR, textStatus, errorThrown) ->
               alert('An error occurred while loading gallery data: ' + textStatus)
         )
      )
</script>

<script src="../lib/underscore-min.js"></script>
<script src="../lib/jquery-min.js"></script>
<script src="../lib/json2.js"></script>
<script src="../lib/backbone-min.js"></script>
<script src="../lib/coffee-script-min.js"></script>

</body>
</html>